; 5. CRUD

#packopt name "crud"
#packopt hide 1

#include "kernel32.as"
#include "user32.as"

#define WM_COMMAND 0x0111

#define EN_CHANGE 0x0300
#define LBN_SELCHANGE 1
#define LB_INSERTSTRING         0x0181
#define LB_DELETESTRING         0x0182
#define LB_GETCURSEL            0x0188

#define ctype LOWORD(%1) ((%1) & 0xFFFF)
#define ctype HIWORD(%1) (((%1) >> 16) & 0xFFFF)

; ================================================

	gsel 0, -1
	screen 1, 420, 300
	title "CRUD"

	; initialize
	person_count = 0
	sdim person_names
	sdim person_surnames

	current_list = ""
	current_index = -1
	current_prefix = ""
	current_name = ""
	current_surname = ""

	; Default values
	person_names(person_count) = "Emil"
	person_surnames(person_count) = "Hans"
	person_count++
	person_names(person_count) = "Mustermann"
	person_surnames(person_count) = "Max"
	person_count++
	person_names(person_count) = "Tisch"
	person_surnames(person_count) = "Roman"
	person_count++

	repeat person_count
		current_list += strf("%s, %s\n", person_names(cnt), person_surnames(cnt))
	loop

	; background: gray
	hsvcolor ,, 0xF3
	boxf

	font "Biz UDGothic", 14
	objmode objmode_usefont
	hsvcolor ,, 0x1B

	; Left:
	; Filter
	pos 10, 15
	mes "Filter prefix:"
	pos 120, 10
	input current_prefix, 90, 25
	prefix_input_id = stat

	; Listbox
	pos 10, 45
	objsize 200, 25
	listbox current_index, 200, current_list
	listbox_id = stat

	; Right:
	pos 240, 50
	mes "Name:"
	pos 310, 45
	input current_name, 100, 25
	name_input_id = stat

	pos 240, 85
	mes "Surname:"
	pos 310, 80
	input current_surname, 100, 25
	surname_input_id = stat

	; Bottom:
	objsize 80, 25
	pos 10, 265
	button gosub "Create", *create_click
	create_button_id = stat

	pos 100, 265
	button gosub "Update", *update_click
	update_button_id = stat

	pos 190, 265
	button gosub "Delete", *delete_click
	delete_button_id = stat

	; TODO: handle WM_SIZING
	oncmd gosub *on_command, WM_COMMAND
	stop

*on_command

	if lparam == objinfo_hwnd(prefix_input_id) {
		if HIWORD(wparam) == EN_CHANGE {
			gosub *update_filter
			return
		}
	}
	if lparam == objinfo_hwnd(listbox_id) {
		if HIWORD(wparam) == LBN_SELCHANGE {
			sendmsg objinfo_hwnd(listbox_id), LB_GETCURSEL
			current_index = stat
			gosub *listbox_select
			return
		}
	}
	return

*update_filter

	; TODO: implement
	return

*listbox_select

	if current_index >= person_count {
		current_index = -1
	}

	if current_index < 0 {
		current_name = ""
		current_surname = ""

		updating = 1
		objprm name_input_id, ""
		objprm surname_input_id, ""
		updating = 0
		return
	}

	current_name = person_names(current_index)
	current_surname = person_surnames(current_index)

	updating = 1
	objprm name_input_id, current_name
	objprm surname_input_id, current_surname
	updating = 0
	return

*create_click

	current_index = person_count

	person_names(person_count) = current_name
	person_surnames(person_count) = current_surname
	person_count++

	updating = 1
	sendmsg objinfo_hwnd(listbox_id), LB_INSERTSTRING, current_index, strf("%s, %s", person_names(current_index), person_surnames(current_index))
	objprm listbox_id, current_index
	updating = 0

	; Update name inputs.
	gosub *listbox_select
	return

*update_click

	if current_index < 0 || current_index >= person_count {
		return
	}

	person_names(current_index) = current_name
	person_surnames(current_index) = current_surname

	updating = 1
	sendmsg objinfo_hwnd(listbox_id), LB_DELETESTRING, current_index
	sendmsg objinfo_hwnd(listbox_id), LB_INSERTSTRING, current_index, strf("%s, %s", current_name, current_surname)
	objprm listbox_id, current_index
	updating = 0
	return

*delete_click

	if current_index < 0 || current_index >= person_count {
		return
	}

	; Remove at index
	repeat person_count - current_index - 1, current_index
		person_names(cnt) = person_names(cnt + 1)
		person_surnames(cnt) = person_surnames(cnt + 1)
	loop
	person_count--

	updating = 1
	sendmsg objinfo_hwnd(listbox_id), LB_DELETESTRING, current_index
	if current_index == person_count {
		current_index--
	}
	if current_index >= 0 {
		objprm listbox_id, current_index
	}
	updating = 0

	; Update name inputs.
	gosub *listbox_select
	return
