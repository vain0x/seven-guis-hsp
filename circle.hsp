; 6. Circle Drawer

#packopt name "circle"

#include "user32.as"

#define WS_CHILD        0x40000000
#define WS_VISIBLE      0x10000000
#define WM_HSCROLL      0x0114
#define WM_USER         0x0400

#define TRACKBAR_CLASS  "msctls_trackbar32"
#const TBM_SETPOS       (WM_USER + 5)
#const TBM_SETRANGE     (WM_USER + 6)
#define TB_THUMBTRACK   5

#define ctype LOWORD(%1) ((%1) & 0xFFFF)
#define ctype HIWORD(%1) (((%1) >> 16) & 0xFFFF)
#define ctype MAKELONG(%1, %2) (((%1) & 0xFFFF) | (((%2) & 0xFFFF) << 16))

; Window IDs
#const k_window_main 0
#const k_window_edit 1
#const k_window_canvas 2

; Change types
#enum k_ct_add = 1
#enum k_ct_resize

; ================================================

	title "CircleDraw"

	circle_count = 0
	dim circle_x
	dim circle_y
	; (diameter)
	dim circle_diam
	; (0: not deleted, 1: deleted)
	dim circle_deleted

	; selected circle index
	selected_ci = -1

	editing = 0
	; editing circle index
	edit_ci = -1
	; old diameter of editing circle
	edit_diam = -1

	undo_count = 0
	redo_count = 0
	dim change_type
	; type add/resize -> (circle index)
	dim change_ci
	; type resize -> changed diam
	dim change_diam

	; Main window:
	font "Biz UDGothic", 14
	objmode objmode_usefont

	pos 10, 10
	objsize 100, 25
	button gosub "Undo", *undo

	pos 120, 10
	button gosub "Redo", *redo

	; Edit dialog:
	screen k_window_edit, 280, 80, screen_hide | screen_fixedsize | screen_tool
	title "Edit - CircleDraw"

	font "Biz UDGothic", 14
	objmode objmode_usefont

	; Trackbar
	pos 10, 45
	winobj TRACKBAR_CLASS, "", 0, WS_CHILD | WS_VISIBLE, 260, 25, 0, 0
	trackbar_id = stat

	sendmsg objinfo_hwnd(trackbar_id), TBM_SETRANGE, 0, MAKELONG(10, 50)

	oncmd gosub *on_hscroll, WM_HSCROLL

	buffer k_window_canvas, 800, 800

	; show main window
	gsel k_window_main, 1

	onexit gosub *on_exit

	; generate default circles
	randomize

	repeat 4
		ci = circle_count
		circle_x(ci) = rnd(ginfo_winx - 40 - 15)
		circle_y(ci) = rnd(ginfo_winy - 55 - 15)
		circle_diam(ci) = 30
		circle_deleted(ci) = 0
		circle_count++
	loop

	; goto *main_loop

*main_loop

	; keep modal
	if ginfo_act == k_window_main && editing {
		gsel k_window_edit, 1
	}

	gsel k_window_main

	; capture state
	window_w = ginfo_winx
	window_h = ginfo_winy

	; compute layout
	margin = 10
	button_h = 25
	canvas_x = margin
	canvas_y = button_h + margin * 2
	canvas_w = window_w - margin * 2
	canvas_h = window_h - canvas_y - margin

	; mouse cursor in canvas
	mx = mousex
	my = mousey

	ok = ginfo_act == k_window_main
	ok &&= ginfo_wx1 <= ginfo_mx && ginfo_mx <= ginfo_wx2
	ok &&= ginfo_wy1 <= ginfo_my && ginfo_my <= ginfo_wy2
	ok &&= canvas_x <= mx && mx <= canvas_x + canvas_w
	ok &&= canvas_y <= my && my <= canvas_y + canvas_h
	if ok {
		mx -= canvas_x
		my -= canvas_y
	} else {
		mx = -100
		my = -100
	}

	; handle input
	stick stick_flags
	left_click = (stick_flags & 0x0100) != 0
	right_click = (stick_flags & 0x0200) != 0

	if left_click && mx >= 0 {
		; add circle
		ci = circle_count
		circle_x(ci) = mx
		circle_y(ci) = my
		circle_diam(ci) = 30
		circle_deleted(ci) = 0
		circle_count++

		; add change
		chi = undo_count
		change_type(chi) = k_ct_add
		change_ci(chi) = ci
		undo_count++
		redo_count = 0
	}

	; compute circles
	if mx >= 0 {
		repeat circle_count
			if circle_deleted(cnt) : continue

			cx = circle_x(cnt)
			cy = circle_y(cnt)
			cr = circle_diam(cnt) / 2 ; radius
			circle_dist(cnt) = powf(cx - mx, 2) + powf(cy - my, 2)
			circle_hover(cnt) = circle_dist(cnt) <= powf(cr, 2)
		loop
	} else {
		repeat circle_count
			circle_dist(cnt) = 1e9
			circle_hover(cnt) = 0
		loop
	}

	; find selected circle (hover & nearest)
	if edit_ci >= 0 {
		selected_ci = edit_ci
	} else {
		nearest_dist = 1.0e9
		nearest_ci = -1
		if mx >= 0 {
			repeat circle_count
				if circle_deleted(cnt) : continue
				if circle_hover(cnt) == 0 : continue

				if nearest_dist > circle_dist(cnt) {
					nearest_dist = circle_dist(cnt)
					nearest_ci = cnt
				}
			loop
		}
		selected_ci = nearest_ci
	}

	if right_click && selected_ci >= 0 {
		edit_ci = selected_ci
		gosub *show_edit_dialog
		gsel k_window_main
	}

	; draw in canvas buffer
	gsel k_window_canvas
	redraw 2

	hsvcolor ,, 0xFF ; white
	boxf 0, 0, canvas_w, canvas_h

	; circles
	repeat circle_count
		render_ci(cnt) = cnt
	loop

	; change rendering order
	if selected_ci >= 0 {
		render_ci(selected_ci) = circle_count - 1
		render_ci(circle_count - 1) = selected_ci
	}

	repeat circle_count
		ci = render_ci(cnt)
		if circle_deleted(ci) : continue

		cx = circle_x(ci)
		cy = circle_y(ci)
		cr = circle_diam(ci) / 2 ; radius

		if selected_ci == ci {
			hsvcolor ,, 0xC9 ; gray (fill)
			circle cx - cr, cy - cr, cx + cr, cy + cr, 1
		}

		hsvcolor ,, 0x1B ; black (border)
		circle cx - cr, cy - cr, cx + cr, cy + cr, 0
	loop

	redraw 1, 0, 0, canvas_w, canvas_h

	; draw in main window
	gsel k_window_main
	redraw 2

	; background: gray
	hsvcolor ,, 0xF3
	boxf

	; foreground: black
	hsvcolor ,, 0x1B

	pos window_w - 100, 10
	mes strf("m=(%d, %d)", mx, my)

	; Canvas:
	hsvcolor ,, 0x1B ; border: black
	boxf margin, canvas_y, margin + canvas_w, canvas_y + canvas_h

	pos margin + 2, canvas_y + 2
	gcopy k_window_canvas, 0, 0, canvas_w - 4, canvas_h - 4

	redraw

	await 16 ; 1000/60
	goto *main_loop

*on_exit

	if wparam == k_window_edit {
		; end edit
		assert editing

		; add change
		chi = undo_count
		change_type(chi) = k_ct_resize
		change_ci(chi) = edit_ci
		change_diam(chi) = edit_diam
		undo_count++
		redo_count = 0

		editing = 0
		edit_ci = -1
		edit_diam = -1
		gsel k_window_edit, -1
		return
	}

	onexit 0
	end

*on_hscroll

	gsel k_window_edit
	if lparam == objinfo_hwnd(trackbar_id) {
		if LOWORD(wparam) == TB_THUMBTRACK {
			track_value = HIWORD(wparam)
			gosub *on_diameter_change
		}
	}
	return

*on_diameter_change

	assert 0 <= edit_ci && edit_ci < circle_count
	assert track_value >= 1
	circle_diam(edit_ci) = track_value
	return

*undo

	if undo_count == 0 : return

	undo_count--
	redo_count++
	chi = undo_count

	if change_type(chi) == k_ct_add {
		; hide circle
		ci = change_ci(chi)
		circle_deleted(ci) = 1
	} else : if change_type(chi) == k_ct_resize {
		; resize circle
		ci = change_ci(chi)
		current_diam = circle_diam(ci)
		circle_diam(ci) = change_diam(chi)
		change_diam(chi) = current_diam
	} else {
		assert
	}
	return

*redo

	if redo_count == 0 : return

	chi = undo_count
	undo_count++
	redo_count--

	if change_type(chi) == k_ct_add {
		; show circle
		ci = change_ci(chi)
		circle_deleted(ci) = 0
	} else : if change_type(chi) == k_ct_resize {
		; resize circle
		ci = change_ci(chi)
		current_diam = circle_diam(ci)
		circle_diam(ci) = change_diam(chi)
		change_diam(chi) = current_diam
	} else {
		assert
	}
	return

*show_edit_dialog

	assert 0 <= edit_ci && edit_ci < circle_count
	assert editing == 0

	editing = 1
	edit_diam = circle_diam(edit_ci)

	gsel k_window_edit
	sendmsg objinfo_hwnd(trackbar_id), TBM_SETPOS, 1, circle_diam(edit_ci)

	gsel k_window_edit, 1

	redraw 2

	; background: gray
	hsvcolor ,, 0xF3
	boxf

	; foreground: black
	hsvcolor ,, 0x1B

	pos 10, 10
	ci = edit_ci
	mes strf("Adjust diameter of circle at (%d, %d).", circle_x(ci), circle_y(ci))

	redraw
	return
